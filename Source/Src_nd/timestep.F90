module timestep_module

  use amrex_fort_module, only : rt => amrex_real
  use c_interface_modules, only : ca_ctoprim

  implicit none

  public

contains

  ! Courant-condition limited timestep

  subroutine ca_estdt(lo,hi,u,u_lo,u_hi,dx,dt) bind(C, name="ca_estdt")

    use network, only: nspec, naux
    use meth_params_module, only: NVAR, QRHO, QU, QV, QW, QREINT, UTEMP, QFS, QFX, do_ctu, NQ, NQAUX
    use eos_module, only: eos
    use eos_type_module, only: eos_t, eos_input_re
    use prob_params_module, only: dim
    use bl_constants_module
#ifdef ROTATION
    use meth_params_module, only: do_rotation, state_in_rotating_frame
    use rotation_module, only: inertial_to_rotational_velocity
    use amrinfo_module, only: amr_time
#endif
    use amrex_fort_module, only : rt => amrex_real

    implicit none

    integer          :: lo(3), hi(3)
    integer          :: u_lo(3), u_hi(3)
    real(rt)         :: u(u_lo(1):u_hi(1),u_lo(2):u_hi(2),u_lo(3):u_hi(3),NVAR)
    real(rt)         :: dx(3), dt, dt_tmp

    real(rt)         :: rhoInv, ux, uy, uz, c, dt1, dt2, dt3
    integer          :: i, j, k

    type (eos_t) :: eos_state

#ifdef ROTATION
    real(rt)         :: vel(3)
#endif

    real(rt)     :: q(u_lo(1):u_hi(1),u_lo(2):u_hi(2),u_lo(3):u_hi(3),NQ)
    real(rt)   :: qaux(u_lo(1):u_hi(1),u_lo(2):u_hi(2),u_lo(3):u_hi(3),NQAUX)

    call ca_ctoprim(lo, hi, &
                      u, u_lo, u_hi, &
                      q,     u_lo, u_hi, &
                      qaux,  u_lo, u_hi, 0)

    ! Call EOS for the purpose of computing sound speed

    do k = lo(3), hi(3)
       do j = lo(2), hi(2)
          do i = lo(1), hi(1)
             rhoInv = ONE / q(i,j,k,QRHO)

             eos_state % rho = q(i,j,k,QRHO )
             eos_state % T   = u(i,j,k,UTEMP)
             eos_state % e   = q(i,j,k,QREINT) * rhoInv
             eos_state % xn  = q(i,j,k,QFS:QFS+nspec-1)
             eos_state % aux = q(i,j,k,QFX:QFX+naux-1)

             call eos(eos_input_re, eos_state)

             ! Compute velocity and then calculate CFL timestep.

             ux = q(i,j,k,QU)
             uy = q(i,j,k,QV)
             uz = q(i,j,k,QW)

#ifdef ROTATION
             if (do_rotation == 1 .and. state_in_rotating_frame /= 1) then
                vel = [ux, uy, uz]
                call inertial_to_rotational_velocity([i, j, k], amr_time, vel)
                ux = vel(1)
                uy = vel(2)
                uz = vel(3)
             endif
#endif

             c = eos_state % cs

             dt1 = dx(1)/(c + abs(ux))
             if (dim >= 2) then
                dt2 = dx(2)/(c + abs(uy))
             else
                dt2 = dt1
             endif
             if (dim == 3) then
                dt3 = dx(3)/(c + abs(uz))
             else
                dt3 = dt1
             endif

             if (do_ctu == 1) then
                dt  = min(dt,dt1,dt2,dt3)
             else
                ! method of lines constraint is tougher
                dt_tmp = ONE/dt1
                if (dim >= 2) then
                   dt_tmp = dt_tmp + ONE/dt2
                endif
                if (dim == 3) then
                   dt_tmp = dt_tmp + ONE/dt3
                endif
                dt = min(dt, ONE/dt_tmp)
             endif

          enddo
       enddo
    enddo

  end subroutine ca_estdt

  ! Check whether the last timestep violated any of our stability criteria.
  ! If so, suggest a new timestep which would not.

  subroutine ca_check_timestep(s_old, so_lo, so_hi, &
                               s_new, sn_lo, sn_hi, &
                               lo, hi, &
                               dx, dt_old, dt_new) &
                               bind(C, name="ca_check_timestep")

    use bl_constants_module, only: HALF, ONE
    use meth_params_module, only: NVAR, QRHO, UTEMP, QREINT, QFS, QFX, QU, QV, QW, &
                                  cfl, do_hydro, NQ, NQAUX
    use prob_params_module, only: dim
    use network, only: nspec, naux
    use eos_module, only: eos
    use eos_type_module, only: eos_input_re, eos_t
    use amrex_fort_module, only : rt => amrex_real

    implicit none

    integer          :: lo(3), hi(3)
    integer          :: so_lo(3), so_hi(3)
    integer          :: sn_lo(3), sn_hi(3)
    real(rt)         :: s_old(so_lo(1):so_hi(1),so_lo(2):so_hi(2),so_lo(3):so_hi(3),NVAR)
    real(rt)         :: s_new(sn_lo(1):sn_hi(1),sn_lo(2):sn_hi(2),sn_lo(3):sn_hi(3),NVAR)
    real(rt)         :: dx(3), dt_old, dt_new

    integer          :: i, j, k
    real(rt)         :: rhoninv
    real(rt)         :: tau_CFL


    real(rt)         :: v(3), c
    type (eos_t)     :: eos_state

    real(rt)     :: q_old(so_lo(1):so_hi(1),so_lo(2):so_hi(2),so_lo(3):so_hi(3),NQ)
    real(rt)     :: q_new(sn_lo(1):sn_hi(1),sn_lo(2):sn_hi(2),sn_lo(3):sn_hi(3),NQ)
    real(rt)   :: qoaux(so_lo(1):so_hi(1),so_lo(2):so_hi(2),so_lo(3):so_hi(3),NQAUX)
    real(rt)   :: qnaux(sn_lo(1):sn_hi(1),sn_lo(2):sn_hi(2),sn_lo(3):sn_hi(3),NQAUX)


    call ca_ctoprim(lo, hi, &
                      s_old, so_lo, so_hi, &
                      q_old,     so_lo, so_hi, &
                      qoaux,  so_lo, so_hi, 0)
    call ca_ctoprim(lo, hi, &
                    s_new, sn_lo, sn_hi, &
                    q_new,     sn_lo, sn_hi, &
                    qnaux,  sn_lo, sn_hi, 0)

    do k = lo(3), hi(3)
       do j = lo(2), hi(2)
          do i = lo(1), hi(1)

             rhoninv = ONE / q_new(i,j,k,QRHO)

             ! CFL hydrodynamic stability criterion

             ! If the timestep violated (v+c) * dt / dx > 1,
             ! suggest a new timestep such that (v+c) * dt / dx <= CFL,
             ! where CFL is the user's chosen timestep constraint.
             ! We don't use the CFL choice in the test for violation
             ! because if we did, then even a small increase in velocity
             ! over the timestep would be enough to trigger a retry
             ! (and empirically we have found that this can happen
             ! basically every timestep, which can greatly increase
             ! the cost of a simulation for not much benefit);
             ! but these types of issues are exactly why a user picks a
             ! safe buffer value like CFL = 0.8 or CFL = 0.5. We only
             ! want to trigger a retry if the timestep strongly violated
             ! the stability criterion.

             if (do_hydro .eq. 1) then

                eos_state % rho = q_new(i,j,k,QRHO )
                eos_state % T   = s_new(i,j,k,UTEMP)
                eos_state % e   = q_new(i,j,k,QREINT) * rhoninv
                eos_state % xn  = q_new(i,j,k,QFS:QFS+nspec-1)
                eos_state % aux = q_new(i,j,k,QFX:QFX+naux-1)

                call eos(eos_input_re, eos_state)

                v = HALF * (q_old(i,j,k,QU:QW) + q_new(i,j,k,QU:QW))

                c = eos_state % cs

                tau_CFL = minval(dx(1:dim) / (c + abs(v(1:dim))))

                if (dt_old > tau_CFL) then
                   dt_new = min(dt_new, cfl * tau_CFL)
                endif

             endif
          enddo
       enddo
    enddo

  end subroutine ca_check_timestep

end module timestep_module
